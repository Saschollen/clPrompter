CPYFRMT:    PGM        PARM(&FROM &TO &FROMMBR &TOMBR &MBROPT &CRTFILE)
            DCL        VAR(&FROM) TYPE(*CHAR) LEN(86)
            DCL        VAR(&FROMFILE) TYPE(*CHAR) STG(*DEFINED) +
                         LEN(10) DEFVAR(&FROM 3)
            DCL        VAR(&FROMLIB) TYPE(*CHAR) STG(*DEFINED) +
                         LEN(10) DEFVAR(&FROM 13)
            DCL        VAR(&FROMSYS) TYPE(*CHAR) STG(*DEFINED) +
                         LEN(64) DEFVAR(&FROM 23)
            DCL        VAR(&TO) TYPE(*CHAR) LEN(86)
            DCL        VAR(&TOFILE) TYPE(*CHAR) STG(*DEFINED) +
                         LEN(10) DEFVAR(&TO 3)
            DCL        VAR(&TOLIB) TYPE(*CHAR) STG(*DEFINED) +
                         LEN(10) DEFVAR(&TO 13)
            DCL        VAR(&TOSYS) TYPE(*CHAR) STG(*DEFINED) +
                         LEN(64) DEFVAR(&TO 23)
            DCL        VAR(&FROMMBR) TYPE(*CHAR) LEN(10)
            DCL        VAR(&TOMBR) TYPE(*CHAR) LEN(10)
            DCL        VAR(&MBROPT) TYPE(*CHAR) LEN(10)
            DCL        VAR(&CRTFILE) TYPE(*CHAR) LEN(4)
            DCL        VAR(&FC) TYPE(*CHAR) LEN(12)
            DCL        VAR(&CPYSRC) TYPE(*CHAR) LEN(512)
            DCL        VAR(&LEN) TYPE(*DEC) LEN(5 0)
            DCL        VAR(&CMDLEN) TYPE(*DEC) LEN(15 5)
            MONMSG     MSGID(CPF0000)

            IF (&TOFILE *EQ '*FROM' *OR &TOFILE = '*FROMFILE') THEN(DO)
               CHGVAR     VAR(&TOFILE) VALUE(&FROMFILE)
            ENDDO
            IF ((&TOLIB *EQ ' ') *OR (&TOLIB *EQ '*FROMLIB')) THEN(DO)
               CHGVAR     VAR(&TOLIB) VALUE(&FROMLIB)
            ENDDO

            DLTF       FILE(QTEMP/DDMTOFILE)
            MONMSG     MSGID(CPF0000)
            DLTF       FILE(QTEMP/DDMFRMFILE)
            MONMSG     MSGID(CPF0000)
            IF (&TOSYS *NE '*LOCAL') THEN(DO)
               CRTDDMF    FILE(QTEMP/DDMTOFILE) +
                            RMTFILE(&TOLIB/&TOFILE) +
                            RMTLOCNAME(&TOSYS *IP)
               CHGVAR     VAR(&TOLIB) VALUE('QTEMP')
               CHGVAR     VAR(&TOFILE) VALUE('DDMTOFILE')
            ENDDO
            IF (&FROMSYS *NE '*LOCAL') THEN(DO)
               CRTDDMF    FILE(QTEMP/DDMFRMFILE) +
                            RMTFILE(&FROMLIB/&FROMFILE) +
                            RMTLOCNAME(&FROMSYS *IP)
               CHGVAR     VAR(&FROMLIB) VALUE('QTEMP')
               CHGVAR     VAR(&FROMFILE) VALUE('DDMFRMFILE')
            ENDDO
            CHGVAR     VAR(&FC) VALUE(X'000000000000000000000000')
            chgvar var(&cpysrc) VALUE('CPYF ' +
                *BCAT 'FROMFILE(' *CAT &FROMLIB *TCAT '/' *CAT &FROMFILE *TCAT ')' +
                *BCAT 'TOFILE(' *CAT &TOLIB *TCAT '/' *CAT &TOFILE *TCAT ') ' +
                *BCAT 'FROMMBR(' *CAT &FROMMBR *TCAT ')' *BCAT 'TOMBR(' *CAT &TOMBR *TCAT ')' +
                *BCAT 'MBROPT(' *CAT &MBROPT *TCAT ')' *BCAT 'CRTFILE(' *CAT &CRTFILE *TCAT ')')

            RTVMSG     MSGID(CPF9897) MSGF(QCPFMSG) MSGDTA(&CPYSRC) +
                         MSGLEN(&LEN)
            CHGVAR     VAR(&CMDLEN) VALUE(&LEN)
            IF (&LEN > 1) THEN(DO)
              SNDPGMMSG  MSGID(CPF9897) MSGF(QCPFMSG) +
                           MSGDTA(%SST(&CPYSRC 1 &LEN))
              CALL       PGM(QCMDEXC) PARM(&CPYSRC &CMDLEN)
            ENDDO

            DLTF       FILE(QTEMP/DDMTOFILE)
            DLTF       FILE(QTEMP/DDMFRMFILE)
ENDPGM:     ENDPGM